//////////////////////////////////////////////////////////////////////////////////////
//  Raylib.h
//////////////////////////////////////////////////////////////////////////////////////

PI :: 3.14159265358979323846;

Color :: struct {
    r: u8;
    g: u8;
    b: u8;
    a: u8;
}

Rectangle :: struct {
    x: float;
    y: float;
    width: float;
    height: float;
}

Vector2 :: struct {
    x: float;
    y: float;
}

// Image, pixel data stored in CPU memory (RAM)
Image :: struct {
    data: *void;             // Image raw data
    width: int;              // Image base width
    height: int;             // Image base height
    mipmaps: int;            // Mipmap levels, 1 by default
    format: int;             // Data format (PixelFormat type)
}

// Texture, tex data stored in GPU memory (VRAM)
Texture :: struct {
    id:      uint;        // OpenGL texture id
    width:   int;         // Texture base width
    height:  int;         // Texture base height
    mipmaps: int;         // Mipmap levels, 1 by default
    format:  int;         // Data format (PixelFormat type)
}

AudioStream :: struct {
    buffer: *void;       // Pointer to internal data used by the audio system
    processor: *void;    // Pointer to internal data processor, useful for audio effects

    sampleRate: uint;    // Frequency (samples per second)
    sampleSize: uint;    // Bit depth (bits per sample): 8, 16, 32 (24 not supported)
    channels: uint;      // Number of channels (1-mono, 2-stereo, ...)
}

Sound :: struct {
    stream: AudioStream;         // Audio stream
    frameCount: uint;            // Total number of frames (considering channels)
}

KeyboardKey :: enum {
    NULL            = 0,        // Key: NULL, used for no key pressed
    // Alphanumeric keys
    APOSTROPHE      = 39,       // Key: '
    COMMA           = 44,       // Key: ,
    MINUS           = 45,       // Key: -
    PERIOD          = 46,       // Key: .
    SLASH           = 47,       // Key: /
    ZERO            = 48,       // Key: 0
    ONE             = 49,       // Key: 1
    TWO             = 50,       // Key: 2
    THREE           = 51,       // Key: 3
    FOUR            = 52,       // Key: 4
    FIVE            = 53,       // Key: 5
    SIX             = 54,       // Key: 6
    SEVEN           = 55,       // Key: 7
    EIGHT           = 56,       // Key: 8
    NINE            = 57,       // Key: 9
    SEMICOLON       = 59,       // Key: ;
    EQUAL           = 61,       // Key: =
    A               = 65,       // Key: A | a
    B               = 66,       // Key: B | b
    C               = 67,       // Key: C | c
    D               = 68,       // Key: D | d
    E               = 69,       // Key: E | e
    F               = 70,       // Key: F | f
    G               = 71,       // Key: G | g
    H               = 72,       // Key: H | h
    I               = 73,       // Key: I | i
    J               = 74,       // Key: J | j
    K               = 75,       // Key: K | k
    L               = 76,       // Key: L | l
    M               = 77,       // Key: M | m
    N               = 78,       // Key: N | n
    O               = 79,       // Key: O | o
    P               = 80,       // Key: P | p
    Q               = 81,       // Key: Q | q
    R               = 82,       // Key: R | r
    S               = 83,       // Key: S | s
    T               = 84,       // Key: T | t
    U               = 85,       // Key: U | u
    V               = 86,       // Key: V | v
    W               = 87,       // Key: W | w
    X               = 88,       // Key: X | x
    Y               = 89,       // Key: Y | y
    Z               = 90,       // Key: Z | z
    LEFT_BRACKET    = 91,       // Key: [
    BACKSLASH       = 92,       // Key: '\'
    RIGHT_BRACKET   = 93,       // Key: ]
    GRAVE           = 96,       // Key: `
    // Function keys
    SPACE           = 32,       // Key: Space
    ESCAPE          = 256,      // Key: Esc
    ENTER           = 257,      // Key: Enter
    TAB             = 258,      // Key: Tab
    BACKSPACE       = 259,      // Key: Backspace
    INSERT          = 260,      // Key: Ins
    DELETE          = 261,      // Key: Del
    RIGHT           = 262,      // Key: Cursor right
    LEFT            = 263,      // Key: Cursor left
    DOWN            = 264,      // Key: Cursor down
    UP              = 265,      // Key: Cursor up
    PAGE_UP         = 266,      // Key: Page up
    PAGE_DOWN       = 267,      // Key: Page down
    HOME            = 268,      // Key: Home
    END             = 269,      // Key: End
    CAPS_LOCK       = 280,      // Key: Caps lock
    SCROLL_LOCK     = 281,      // Key: Scroll down
    NUM_LOCK        = 282,      // Key: Num lock
    PRINT_SCREEN    = 283,      // Key: Print screen
    PAUSE           = 284,      // Key: Pause
    F1              = 290,      // Key: F1
    F2              = 291,      // Key: F2
    F3              = 292,      // Key: F3
    F4              = 293,      // Key: F4
    F5              = 294,      // Key: F5
    F6              = 295,      // Key: F6
    F7              = 296,      // Key: F7
    F8              = 297,      // Key: F8
    F9              = 298,      // Key: F9
    F10             = 299,      // Key: F10
    F11             = 300,      // Key: F11
    F12             = 301,      // Key: F12
    LEFT_SHIFT      = 340,      // Key: Shift left
    LEFT_CONTROL    = 341,      // Key: Control left
    LEFT_ALT        = 342,      // Key: Alt left
    LEFT_SUPER      = 343,      // Key: Super left
    RIGHT_SHIFT     = 344,      // Key: Shift right
    RIGHT_CONTROL   = 345,      // Key: Control right
    RIGHT_ALT       = 346,      // Key: Alt right
    RIGHT_SUPER     = 347,      // Key: Super right
    KB_MENU         = 348,      // Key: KB menu
    // Keypad keys
    KP_0            = 320,      // Key: Keypad 0
    KP_1            = 321,      // Key: Keypad 1
    KP_2            = 322,      // Key: Keypad 2
    KP_3            = 323,      // Key: Keypad 3
    KP_4            = 324,      // Key: Keypad 4
    KP_5            = 325,      // Key: Keypad 5
    KP_6            = 326,      // Key: Keypad 6
    KP_7            = 327,      // Key: Keypad 7
    KP_8            = 328,      // Key: Keypad 8
    KP_9            = 329,      // Key: Keypad 9
    KP_DECIMAL      = 330,      // Key: Keypad .
    KP_DIVIDE       = 331,      // Key: Keypad /
    KP_MULTIPLY     = 332,      // Key: Keypad *
    KP_SUBTRACT     = 333,      // Key: Keypad -
    KP_ADD          = 334,      // Key: Keypad +
    KP_ENTER        = 335,      // Key: Keypad Enter
    KP_EQUAL        = 336,      // Key: Keypad =
}

// Mouse buttons
MouseButton :: enum {
    LEFT    = 0,       // Mouse button left
    RIGHT   = 1,       // Mouse button right
    MIDDLE  = 2,       // Mouse button middle (pressed wheel)
    SIDE    = 3,       // Mouse button side (advanced mouse device)
    EXTRA   = 4,       // Mouse button extra (advanced mouse device)
    FORWARD = 5,       // Mouse button forward (advanced mouse device)
    BACK    = 6,       // Mouse button back (advanced mouse device)
}


// Image loading functions
// NOTE: These functions do not require GPU access
LoadImage :: (fileName: string) -> Image #extern "C"

// Texture loading functions
// NOTE: These functions require GPU access
LoadTexture          :: (fileName: string) -> Texture #extern "C"
UnloadTexture        :: (texture: Texture) #extern "C"
LoadTextureFromImage :: (image: Image) -> Texture #extern "C"

// Window-related functions
InitWindow        :: (width: int, height: int, title: string) #extern "C"
CloseWindow       :: () #extern "C"
WindowShouldClose :: () -> bool #extern "C"
BeginDrawing      :: () #extern "C"
EndDrawing        :: () #extern "C"
ClearBackground   :: (color: Color) #extern "C"

// Drawing functions
DrawFPS  :: (posX: int, posY: int) #extern "C"                                                  // Draw current FPS
DrawText :: (text: string, posX: int, posY: int, fontSize: int, color: Color) #extern "C"       // Draw text (using default font)
// DrawTextEx :: (Font font, const char *text, Vector2 position, float fontSize, float spacing, Color tint); // Draw text using font and additional parameters
// DrawTextPro :: (Font font, const char *text, Vector2 position, Vector2 origin, float rotation, float fontSize, float spacing, Color tint); // Draw text using Font and pro parameters (rotation)
// DrawTextCodepoint :: (Font font, int codepoint, Vector2 position, float fontSize, Color tint); // Draw one character (codepoint)
// DrawTextCodepoints :: (Font font, const int *codepoints, int count, Vector2 position, float fontSize, float spacing, Color tint); // Draw multiple character (codepoint)
DrawTexture :: (texture: Texture, posX: int, posY: int, tint: Color) #extern "C"

// Basic shapes drawing functions
DrawCircle           :: (centerX: int, centerY: int, radius: float, color: Color) #extern "C"
DrawRectangle        :: (posX: int, posY: int, width: int, height: int, color: Color) #extern "C"
DrawRectangleRec     :: (rec: Rectangle, color: Color) #extern "C"
DrawRectangleLines   :: (posX: int, posY: int, width: int, height: int, color: Color) #extern "C"
DrawRectangleLinesEx :: (rec: Rectangle, lineThick: float, color: Color) #extern "C"

// Basic shapes collision detection functions
CheckCollisionRecs :: (rec1: Rectangle, rec2: Rectangle) -> bool #extern "C"                                           // Check collision between two rectangles

// Text font info functions
MeasureText :: (text: string, fontSize: int) -> int #extern "C"

// Timing-related functions
SetTargetFPS :: (fps: int) #extern "C"
GetFrameTime :: () -> f32 #extern "C"
GetTime      :: () -> f64 #extern "C"

// Input-related functions: keyboard
IsKeyPressed  :: (key: KeyboardKey) -> bool #extern "C"
IsKeyDown     :: (key: KeyboardKey) -> bool #extern "C"
GetKeyPressed :: () -> KeyboardKey #extern "C"
GetMousePosition :: () -> Vector2 #extern "C"

// Input-related functions: mouse
IsMouseButtonPressed :: (button: MouseButton) -> bool #extern "C"                  // Check if a mouse button has been pressed once

// Files management functions
GetWorkingDirectory :: () -> string #extern "C"

// Misc. functions
GetRandomValue :: (min: int, max: int) -> int #extern "C"

// Wave/Sound management functions
PlaySound :: (sound: Sound) #extern "C"                                    // Play a sound
StopSound :: (sound: Sound) #extern "C"                                    // Stop playing a sound

// Audio device management functions
InitAudioDevice :: () #extern "C"                                     // Initialize audio device and context
CloseAudioDevice :: () #extern "C"                                    // Close the audio device and context
IsAudioDeviceReady :: () -> bool #extern "C"                          // Check if audio device has been initialized successfully
SetMasterVolume :: (volume: float) #extern "C"                        // Set master volume (listener)

LoadSound :: (fileName: string) -> Sound #extern "C"
UnloadSound :: (sound: Sound) #extern "C"

//////////////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////////////
//  C math.h functions
//////////////////////////////////////////////////////////////////////////////////////
round :: (value: f64) -> f64 #extern "C"
sin   :: (angle: f64) -> f64 #extern "C"
cos   :: (angle: f64) -> f64 #extern "C"
sqrt  :: (value: f64) -> f64 #extern "C"

//////////////////////////////////////////////////////////////////////////////////////
//  C stdlib functions
//////////////////////////////////////////////////////////////////////////////////////
sprintf :: (str: *u8, format: *u8, arg0: int) -> int #extern "C"






//////////////////////////////////////////////////////////////////////////////////////




BALL_SIZE :: 20;
BRICK_WIDTH  :: 100;
BRICK_HEIGHT :: 30;
BRICK_PADDING :: 20;
BRICK_ROWS :: 7;
BRICK_COLS :: 8;
PICKUP_SPAWN_CHANCE :: 25; // in percentage
PICKUP_SIZE :: 26;

INITIAL_BAR_WIDTH     :: 100;
INITIAL_BALL_VELOCITY :: 550.0;

TOP_MARGIN :: 80;
WINDOW_WIDTH :: 1200;
WINDOW_HEIGHT :: 800;




Brick :: struct {
    x:     int;
    y:     int;
    color: Color;
    dead:  bool;
}

Bar :: struct {
    x:      float;
    y:      float;
    width:  float;
    height: float;
}

Ball :: struct {
    x:      float;
    y:      float;
    size:   float;
    velX:  float;
    velY:  float;
}

Player :: struct {
    score: int;
    lives: int;
    isHoldingBall: bool;
    hasFireballPowerup: bool;
    fireballPowerupDuration: int;
}

PickupType :: enum {
    BallSpeedUp,
    BallSlowDown,
    BarWiden,
    BarShorten,
    Fireball,
    COUNT,
}

Pickup :: struct {
    type: PickupType;
    x: float;
    y: float;
    velY: float;
    isShown: bool;
}

PickupSpawner :: struct {
    pickups: []Pickup;
    freeIndex: int;
}

getRect :: method (ball: Ball) -> Rectangle {
    return {
        ball.x,
        ball.y,
        ball.size,
        ball.size,
    };
}

getRect :: method (bar: Bar) -> Rectangle {
    return {
        bar.x,
        bar.y,
        bar.width,
        bar.height,
    };
}

main :: () {
    COLOR_WHITE := Color{255, 255, 255, 255};

    InitWindow(WINDOW_WIDTH, WINDOW_HEIGHT, "Super Breakout");
    InitAudioDevice();
    SetTargetFPS(90);

    //
    // Textxures
    //
    heartTexture := LoadTexture("data/textures/heart.png");
    heartTexture.width  = 24;
    heartTexture.height = 24;


    pickupTextures : [PickupType.COUNT]Texture = [
        LoadTexture("data/textures/pickups/ball_speed_up.png"),
        LoadTexture("data/textures/pickups/ball_slow_down.png"),
        LoadTexture("data/textures/pickups/bar_widen.png"),
        LoadTexture("data/textures/pickups/bar_shorten.png"),
        LoadTexture("data/textures/pickups/fire_ball.png"),
    ];

    // Scale each pickup texture
    for 0..pickupTextures.count {
        texture := &pickupTextures[it];
        texture.width  = PICKUP_SIZE;
        texture.height = PICKUP_SIZE;
    }


    //
    // Sounds
    //
    soundBallBounce1   := LoadSound("data/sounds/ball_bounce_1.mp3");
    soundBallBounce2   := LoadSound("data/sounds/ball_bounce_2.wav");
    soundPickup        := LoadSound("data/sounds/pickup.wav");
    soundGameOver      := LoadSound("data/sounds/game_over.wav");
    soundLostLife      := LoadSound("data/sounds/lost_life.wav");
    soundLevelComplete := LoadSound("data/sounds/level_complete.mp3");

    //
    // Game state + variables
    //
    ball := Ball{
        x = 4,
        y = 4,
        size = BALL_SIZE,
        velX = INITIAL_BALL_VELOCITY,
        velY = INITIAL_BALL_VELOCITY,
    };

    bar := Bar{
        x = WINDOW_WIDTH * 0.5 - INITIAL_BAR_WIDTH * 0.5,
        y = WINDOW_HEIGHT - 70,
        width = INITIAL_BAR_WIDTH,
        height = 15,
    };

    player := Player{
        score = 0,
        lives = 3,
        isHoldingBall = true,
    };

    deadZone := Rectangle{0, WINDOW_HEIGHT - (WINDOW_HEIGHT - (bar.y + bar.height)), WINDOW_WIDTH, 100};

    scoreText: [16]u8;

    brickColors: [BRICK_ROWS]Color = [
        {195, 82, 123, 255},
        {207, 68, 69, 255},
        {198, 99, 49, 255},
        {203, 159, 9, 255},
        {61, 157, 57, 255},
        {52, 60, 210, 255},
        {116, 65, 209, 255},
        // {3, 184, 187, 255},
    ];

    pickups: [16]Pickup;
    pickupSpawner := PickupSpawner{pickups = pickups};
    
    // Initiate the bricks
    bricks: [BRICK_ROWS * BRICK_COLS] Brick;

    brick_row_width := (BRICK_WIDTH + BRICK_PADDING) * BRICK_COLS - 1;

    for row in 0..BRICK_ROWS {
        for col in 0..BRICK_COLS {
            index := row * BRICK_COLS + col;
            
            x := (BRICK_WIDTH + BRICK_PADDING)  * col + (WINDOW_WIDTH / 2) - (brick_row_width / 2);
            y := (BRICK_HEIGHT + BRICK_PADDING) * row + TOP_MARGIN;

            color := brickColors[row];

            brick := Brick{
                x = x,
                y = y,
                color = color,
                dead = false,
            };

            bricks[index] = brick;
        }
    }

    // Math for bouncing the ball with the bar at a certain angle
    ANGLE_MIN := degreeToRadians(15.0);
    ANGLE_MAX := degreeToRadians(165.0);

    //
    // Main game-loop
    //
    while !WindowShouldClose() {

        dt := GetFrameTime();

        //
        // Input
        //

        mouse_pos := GetMousePosition();

        bar.x = mouse_pos.x - bar.width * 0.5;

        if bar.x < 0 {
            bar.x = 0;
        }
        if bar.x > WINDOW_WIDTH - bar.width {
            bar.x = xx WINDOW_WIDTH - bar.width;
        }

        if IsMouseButtonPressed(.LEFT) {
            player.isHoldingBall = false;
        }

        //
        // Hit detection
        //

        // Simulate the next ball position
        nextBall  := ball;
        nextBall.x = ball.x + ball.velX * dt;
        nextBall.y = ball.y + ball.velY * dt;
        
        // Collisions ball and walls
        if nextBall.x < 0 || nextBall.x + nextBall.size > WINDOW_WIDTH {
            ball.velX = -ball.velX;
            PlaySound(soundBallBounce2);
        }

        if nextBall.y < 0 || nextBall.y + nextBall.size > WINDOW_HEIGHT {
            ball.velY = -ball.velY;
            PlaySound(soundBallBounce2);
        }

        // Collision ball and bar
        nextBallRect := nextBall.getRect();
        barRect      := bar.getRect();

        if !player.isHoldingBall && CheckCollisionRecs(nextBallRect, barRect) {

            // The outbound angle of the ball when hitting the bar, follows that of a half circle.
            // If the ball hits the far left of the bar, the out angle is 20 degree
            // If the ball hits the far right of the bar, the out angle is 160 degree
            // Everything in between, we lerp between those two angles, hence, if the ball hits
            // the middle of the bar, the ball will go straight up in a 90 degree angle
            dx := nextBallRect.x + nextBallRect.width * 0.5 - barRect.x;

            t := dx / barRect.width;

            // Clamp t
            if t < 0 { t = 0; }
            if t > 1.0 { t = 1.0; }

            theta := lerp(ANGLE_MIN, ANGLE_MAX, t);

            directionVector := Vector2{
                x = - xx cos(xx theta),
                y = - xx sin(xx theta),
            };

            speed := cast(f32) sqrt(xx (ball.velX*ball.velX + ball.velY*ball.velY));

            ball.velX = directionVector.x * speed;
            ball.velY = directionVector.y * speed;

            PlaySound(soundBallBounce2);
        }

        // Collision ball and bricks
        nextBallRect = nextBall.getRect();
        
        for row in 0..BRICK_ROWS {
            for col in 0..BRICK_COLS {
                index := row * BRICK_COLS + col;
                brick := &bricks[index];

                if brick.dead { continue; } // mehhhh

                brick_rect := Rectangle{xx brick.x, xx brick.y, BRICK_WIDTH, BRICK_HEIGHT};
                if CheckCollisionRecs(nextBallRect, brick_rect) {

                    if player.hasFireballPowerup {
                        // Skip the bounce
                    } else {
                        bounceBall(&ball, brick_rect);
                    }

                    brick.dead = true;
                    player.score += 100;

                    PlaySound(soundBallBounce1);

                    maybeSpawnRandomPickup(&pickupSpawner, {xx brick.x, xx brick.y});

                    break;
                }
            }
        }

        // Collision bar and pickups
        {
            barRect := bar.getRect();

            for i in 0..pickups.count {
                pickup := &pickups[i];
                if !pickup.isShown { continue; }

                pickupRect := Rectangle{pickup.x, pickup.y, PICKUP_SIZE, PICKUP_SIZE};

                if CheckCollisionRecs(barRect, pickupRect) {
                    handleOnPickup(pickup, &bar, &ball, &player, soundPickup);
                }
            }
        }
        

        // Collision ball and dead zone
        ballRect := ball.getRect();
        if CheckCollisionRecs(ballRect, deadZone) {
            player.isHoldingBall = true;
            player.lives -= 1;

            if player.lives <= 0 {
                PlaySound(soundGameOver);
                print("You completely lost!");
            } else {
                PlaySound(soundLostLife);

                // Reset and remove all powerup effects
                player.hasFireballPowerup = false;
                bar.width = INITIAL_BAR_WIDTH;
                ball.velX = INITIAL_BALL_VELOCITY;
                ball.velY = INITIAL_BALL_VELOCITY;
            }
        }
        

        // Ball updates
        if player.isHoldingBall {
            ball.x = bar.x + bar.width * 0.5 - BALL_SIZE * 0.5;
            ball.y = bar.y - BALL_SIZE - 4;
        } else {
            // Follow normal ball behaviour
            ball.x += ball.velX * dt;
            ball.y += ball.velY * dt;
        }

        updatePlayerEffects(&player);
        updatePickupPositions(pickups);


        //
        // Drawing
        //
        BeginDrawing();
        ClearBackground({ 30, 30, 30, 30 });

        // Draw pickups
        for i in 0..pickups.count {
            pickup := &pickups[i];

            if !(pickup.isShown) { 
                continue; 
            }

            texture := pickupTextures[pickup.type];

            DrawRectangle(xx pickup.x, xx pickup.y, texture.width, texture.height, Color{50, 50, 50, 255});
            DrawTexture(texture, xx pickup.x, xx pickup.y, Color{255, 255, 255, 255});
        }
        
        // Draw bricks
        for b in bricks {
            if b.dead {
                continue;
            }

            BORDER_THICKNESS :: 3;
            borderColor := b.color; 
            borderColor.a = 170;

            borderRect := Rectangle{xx b.x, xx b.y, BRICK_WIDTH, BRICK_HEIGHT};

            DrawRectangle(b.x + BORDER_THICKNESS, b.y + BORDER_THICKNESS, BRICK_WIDTH - 2 * BORDER_THICKNESS, BRICK_HEIGHT - 2 * BORDER_THICKNESS, b.color);
            DrawRectangleLinesEx(borderRect, BORDER_THICKNESS, borderColor);
        }

        // Draw the ball
        {
            ballColor := COLOR_WHITE;
            if player.hasFireballPowerup {
                ballColor = {255, 20, 20, 255};
            }
            DrawRectangle(xx ball.x, xx ball.y, xx ball.size, xx ball.size, ballColor);
        }
        
        // Draw the bar
        DrawRectangle(xx bar.x, xx bar.y, xx bar.width, xx bar.height, COLOR_WHITE);

        DrawFPS(WINDOW_WIDTH - 100, 16);

        // Draw lives
        for 0..player.lives {
            padX := 6.0;
            posX := cast(int) (10.0 + (heartTexture.width + padX) * it);

            DrawTexture(heartTexture, posX, 16, Color{255, 255, 255, 255});
        }

        // Draw score
        sprintf(scoreText.data, xx "Score: %d", player.score);
        DrawText(xx scoreText.data, 105, 16, 24, COLOR_WHITE);

        
        // DrawRectangleRec(deadZone, {255, 0, 0, 30});

        EndDrawing();
    }

    UnloadTexture(heartTexture);

    for pickupTextures {
        UnloadTexture(it);
    }

    CloseAudioDevice();
    CloseWindow();
}

handleOnPickup :: (pickup: *Pickup, bar: *Bar, ball: *Ball, player: *Player, pickup_sound: Sound) {
    pickup.isShown = false;

    if pickup.type == .BallSpeedUp {
        ball.velX *= 1 + 0.2;
        ball.velY *= 1 + 0.2;
    }
    else if pickup.type == .BallSlowDown {
        ball.velX *= 1 - 0.2;
        ball.velY *= 1 - 0.2;
    }
    else if pickup.type == .BarWiden {
        bar.width += 20;
    }
    else if pickup.type == .BarShorten {
        bar.width -= 20;
    }
    else if pickup.type == .Fireball {
        player.hasFireballPowerup      = true;
        player.fireballPowerupDuration = 1500;
    }

    PlaySound(pickup_sound);
}

updatePickupPositions :: (pickups: []Pickup) {
    dt := GetFrameTime();

    for i in 0..pickups.count {
        p := &pickups[i];

        if p.isShown {
            p.y += p.velY * dt;
        }

        if p.y > WINDOW_HEIGHT {
            p.isShown = false;
        }
    }
}

updatePlayerEffects :: (player: *Player) {
    dtMilliseconds := GetFrameTime() * 1000;

    if player.hasFireballPowerup {
        
        player.fireballPowerupDuration -= xx dtMilliseconds;

        if player.fireballPowerupDuration <= 0 {
            player.hasFireballPowerup = false;
        }
    }
}

maybeSpawnRandomPickup :: (ps: *PickupSpawner, pos: Vector2) {

    r := GetRandomValue(0, 100);
    if !(r > 100 - PICKUP_SPAWN_CHANCE) {
        return;
    }

    index := GetRandomValue(0, xx PickupType.COUNT - 1);

    pickup := Pickup{
        type = cast(PickupType) index,
        x = pos.x,
        y = pos.y,
        velY = xx GetRandomValue(80, 120),
        isShown = true,
    };
    
    ps.pickups[ps.freeIndex] = pickup;
    ps.freeIndex += 1;
}

bounceBall :: (ball: *Ball, b: Rectangle) {
    overlap := Vector2{};
    a       := ball.getRect();

    // Figure out how much we overlap with the rectangle
    if ball.velX > 0 {
        overlap.x = a.x + a.width - b.x; 
    } else {
        overlap.x = b.x + b.width - a.x; 
    }

    if ball.velY > 0 {
        overlap.y = a.y + a.height - b.y; 
    } else {
        overlap.y = b.y + b.height - a.y; 
    }

    // Adjust direction and position of the ball to make it unstuck
    if overlap.x < overlap.y {
        ball.velX = -ball.velX;
        ball.x    += overlap.x;
    } else {
        ball.velY = -ball.velY;
        ball.y    += overlap.y;
    }
}

degreeToRadians :: (deg: f32) -> f32 {
    return deg * (PI / 180.0);
}

lerp :: (a: f32, b: f32, t: f32) -> f32 {
    return a + (b - a) * t;
}